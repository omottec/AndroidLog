apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.0'
    testCompile 'junit:junit:4.12'
}

group = "com.github.omottec"
archivesBaseName = 'android-log'
version = '0.0.14'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'AndroidLog'
            description = 'A lib for android log'
            url WEB_SITE_URL
            inceptionYear '2016'

            packaging 'aar'
            groupId 'com.github.omottec'
            artifactId 'android-log'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id USER
                    name 'Daniel Bond'
                    email EMAIL
                }
            }
            scm {
                connection GIT_URL
                developerConnection GIT_URL
                url WEB_SITE_URL
            }
        }
    }
}

task sourcesJar(type: Jar) {
    baseName = archivesBaseName
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName = archivesBaseName
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = USER
    key = BINTRAY_API_KEY
    configurations = ['archives']
    publish = true
    pkg {
        repo = "maven"
        name = "com.github.omottec:android-log"
        websiteUrl = WEB_SITE_URL
        vcsUrl = GIT_URL
        issueTrackerUrl = "$WEB_SITE_URL/issues"
        licenses = ["Apache-2.0"]

        version {
            name = version
            released = new Date()
//            vcsTag = ''

            gpg {
                sign = true
                passphrase = GPG_PASSPHRASE
            }

            mavenCentralSync {
                sync = true
                user = USER
                password = MAVEN_CENTRAL_PWD
                close = '1'
            }
        }
    }
}